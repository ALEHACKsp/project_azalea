# SConstruct file for the main kernel.

import os
import defaults

# Setup environment.
env = Environment()
defaults.setup_default_env(env)
env['CPPFLAGS'] = '-mno-red-zone -nostdlib -nostartfiles -nodefaultlibs -mcmodel=large -fno-exceptions -fno-rtti'
env['LINKFLAGS'] = "-T build_support/kernel_stage.ld"
env['LINK'] = 'ld -Map /tmp/kernel_map.map'

# List of required components.
dependencies_in = ['#entry/SConscript',
                   '#processor/SConscript',
                   '#klib/memory/SConscript',
                   '#klib/panic/SConscript',
                   '#mem/SConscript',
                   '#klib/c_helpers/SConscript',
                   '#klib/misc/SConscript',
                   '#klib/lists/SConscript',
                   '#klib/tracing/SConscript',
                   '#klib/synch/SConscript',
                   '#syscall/SConscript',
                   '#acpi/SConscript-ACPICA',
                   '#acpi/SConscript',
                  ]
                   
# Build components.
dependencies_out = [ ]

for dep in dependencies_in:
  out_dir = os.path.join('output', dep.strip('#'))
  dep_out = SConscript(dep, 'env', variant_dir=out_dir, duplicate=0)
  dependencies_out.append(dep_out)

env.Program("output/kernel64.sys", dependencies_out)